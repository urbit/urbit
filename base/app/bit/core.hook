::
::
::::
  :: 
/?  314
/-  *talk
/+  talk, sole, http
!:
::::  sivtyv-barnel
  ::  be sure to have oauth2-code markk
|%
++  axle  cred
++  cred
$:  client-id=(unit ,@t)   app-secret=(unit ,@t)
    oauth-code=(unit ,@t)  auth-token=(unit ,@t)   
    refr-token=(unit ,@t)  contacts=(map ,@t ,@t)
    outgoing=(map ship ,@t)  incoming=(map ,@t ship)
==
++  move  ,[bone (mold note gift)]
++  hapt  ,[ship path]
++  note
  $%  [%g %mess hapt ship cage]
      [%e %them (unit hiss)]
  ==
++  gift
  $%  [%nice ~]
      [%mean ares]
  ==
++  sign
  $%  $:  %e
  $%  [%thou p=httr]
  ==  ==
  [%g %nice ~]
  ==
--
::
!:
|_  [hid=hide vat=axle]
++  prep  ,_`.
++  redirect-uri  (crip (weld hostname "/~~/home/pub/bit/fab"))
++  hostname  
  ?+  (clan our.hid)  !!
     %czar  "http://104.236.139.191:{?+(our.hid !! %~zod "8443", %~reg "8444")}"
     %duke  "http://{+:<our.hid>}.urbit.org"
  ==
++  auth-url
  %+  weld  "https://www.coinbase.com/oauth/authorize"
  %-  tail:earn  :~
    'response_type'^%code
    'client_id'^(need client-id.vat)
    'redirect_uri'^redirect-uri
    'scope'^'user balance buy sell send transactions addresses'
    'meta[send_limit_amount]'^'1'
    'meta[send_limit_curency]'^'BTC'
    'meta[send_limit_period]'^'day'
  ==
++  api-call
  |=  [ost=bone pour-path=wire end-point=path req=$|(%get [%post p=json])]
  ^-  move
  %^  httpreq  ost  pour-path 
  [/com/coinbase/api v1/end-point req ~ ['access_token' (need auth-token.vat)]~]
::
++  callback-uri  (weld hostname "/~/to/{(trip app.hid)}/json.json?anon&wire=/")
++  poke-auth
  |=  [ost=bone you=ship arg=[secr=cord id=cord]]
  =:  app-secret.vat  [~ secr.arg]
      client-id.vat   [~ id.arg]
    ==
  =+  prl=`purl`(need (epur (crip auth-url)))
  :_  +>.$  
  :+    %^  publish  ost  you
        :~  [%lin & 'secret and client id saved successfully']
            [%lin & 'please click on the url below to continue authentication']
            [%url prl] 
        ==
    [ost %give %nice ~]
  ~
::
++  send-message                      ::  inter-app interface
  |=  [ost=bone pax=path his=ship mez=$%([%bit-addr-request ~] [%bit-addr-made @t])]
  ^-  move
  =+  vax=(spec !>(mez))
  [ost %pass pax `note`[%g %mess [his /[app.hid]] our.hid `cage`[-.mez (slot 3 vax)]]]
::
++  poke--send                        ::  send to ship
  |=  [ost=bone @ to=ship amount=@t]
  =.  outgoing.vat  (~(put by outgoing.vat) to amount)
  :_  +>.$
  :_  [ost %give %nice ~]~
  (send-message ost /sent to %bit-addr-request ~)
++  poke-bit-addr-request             ::  give your address to receive bit
  |=  [ost=bone his=ship req=~]           ::  gen new address with callback
  :_  +>.$
  :_  [ost %give %nice ~]~
  %^  api-call  ost  /addr-request/(scot %p his) 
  :+  /addresses  %post
  %+  joba  %address
  %-  jobe  :~
    label/(jape "address for {<his>}")
    'callback_url'^(jape callback-uri)
  ==
  ::
++  pour-addr-request             ::  send new address to sender
  |=  [ost=bone pax=path rez=json]
  =+  adr=(need ((ot address/so ~):jo rez))
  =+  his=(slav %p +<.pax)
  ~!  incoming.vat
  =.  incoming.vat
    (~(put by incoming.vat) adr his) 
  :_  +>.$
  :_  ~
  (send-message ost [%message ~] his %bit-addr-made adr)
::
++  poke-bit-addr-made             ::  receive address for sending
  |=  [ost=bone his=ship addr=@t]
  :_  +>.$
  :_  [ost %give %nice ~]~
  =+  amount=(~(got by outgoing.vat) his)             ::  get amount to send
  =+  info=[to/s/addr amount/s/amount ~]
  =+  jon=(joba %transaction (jobe info))
  (api-call ost /send /transactions/'send_money' %post jon)
  ::
++  poke-oauth2-code
  |=  [ost=bone you=ship code=cord]
  ~&  'oauth poked'
  =.  oauth-code.vat
    [~ code]
  ?<  |(?=(~ client-id.vat) ?=(~ oauth-code.vat))
  =+  ^=  queries
  :~  ['grant_type' 'authorization_code']
      ['code' (need oauth-code.vat)]
      ['redirect_uri' redirect-uri]
      ['client_id' (need client-id.vat)]
      ['client_secret' (need app-secret.vat)]
  ==
  :_  +>.$
  :+  [ost %give %nice ~]
    (httpreq ost /auth-token /com/coinbase /oauth/token [%post ~] ~ queries)
  ~
++  poke-buy
  |=  [ost=bone you=ship arg=[amount=@t currency=@t]]
  ~&  'buy boked'
  ?<  =(~ amount.arg)
  =+  query=['access_token' (need auth-token.vat)]~
  =+  ^=  pst
      ?:  =(~ currency.arg)
        (jobe qty/s/amount.arg ~)
     (jobe qty/s/amount.arg currency/s/currency.arg ~)
      :_  +>.$
      :+  [ost %give %nice ~]
      (httpreq ost /buy /com/coinbase/api /v1/buys [%post pst] ~ query)
      ~
::
++  poke-list
  |=  [ost=bone you=ship arg=~]
  =+  query=['access_token' (need auth-token.vat)]~
  :_  +>.$
  :+  [ost %give %nice ~]
    (httpreq ost /list /com/coinbase/api /v1/accounts %get ~ query)
  ~
::
++  poke-sell
  |=  [ost=bone you=ship arg=[amount=@t currency=@t]] 
  ?<  =(amount.arg ~)
  ~&  'haven\'t crashed'
  =+  query=['access_token' (need auth-token.vat)]~
  =+  ^=  pst
      ?:  =(~ currency.arg)
        (jobe qty/s/amount.arg currency/s/currency.arg ~)
      (jobe qty/s/amount.arg ~)
      :_  +>.$
      :+  [ost %give %nice ~]~
        (httpreq ost /buy /com/coinbase/api /v1/sells [%post pst] ~ query)
      ~
::
++  poke-send-raw
  |=  $:  ost=bone  you=ship
      arg=[to=@t amone=(unit ,@t) cur=(unit ,@t) amtwo=(unit ,@t) nt=(unit ,@t)]
      ==
  =+  adr=?~((~(get by contacts.vat) to.arg) to ~&('contact-exists' (~(get by contacts.vat) to.arg))) 
  =+  query=['access_token' (need auth-token.vat)]~
  =+  ^=  info
      ?~  cur.arg  
        [to/s/to.arg amount/s/(need amone.arg) ~]
      ?~  nt.arg
        [to/s/to.arg 'amount_currency_iso'^s/(need cur.arg) 'amount_string'^s/(need amtwo.arg) ~]
      [to/s/to.arg amount/s/(need amtwo.arg) 'amount_string'^s/(need amtwo.arg) ~]
  =+  ^=  pst
      (jobe transaction/(jobe info) ~)  
  :_  +>.$
  :+  [ost %give %nice ~]
    (httpreq ost /send /com/coinbase/api /v1/transactions/'send_money' [%post pst] ~ query)
  ~
::
++  poke-json
  |=  [ost=bone you=ship arg=json]
      ~&  arg
      :_  +>.$
      =+  [adr amo]=(need %.(arg (ot address/so amount/no ~):jo))
      =+  frm=(~(get by incoming.vat) adr)
      :_  [ost %give %nice ~]~
      ~!  frm
      (publish ost our.hid [%lin & (crip "just received {(trip amo)} BTC from {<frm>}")]~)
      ::(api-call ost /get-id /transactions/[transaction-id] %get)
++  pour-get-id             ::  display transaction info
  |=  [ost=bone pax=path rez=json]
  :_  +>.$
  :_  ~
  !! 
::
++  pour
  |=  [ost=bone pax=path res=sign]
  ?+  -.pax  !!
    %auth-token
      ?>  ?=(%e -.res)
      ~|  'must receive a 200'^oauth-code.vat
      ~|  res
      ?>  =(200 p.p.res)
      =+  resp=(rash q:(need r.p.res) apex:poja)
      =+  parsed=(need ((ot 'refresh_token'^so 'access_token'^so ~):jo resp))
      ~&  parsed
      =:  auth-token.vat  [~ +.parsed]
          refr-token.vat  [~ -.parsed]
        ==
      ~&  [auth-token.vat refr-token.vat]
      :_  +>.$  [ost %give %nice ~]~
    %message  `+>.$
    ?(%addr-request %get-id)
      ?>  ?=(%e -.res)
      ~|  'must receive a 200'
      ~|  res
      ?>  =(200 p.p.res)
      %-  ?-(-.pax %addr-request pour-addr-request, %get-id pour-get-id)
      [ost pax (rash q:(need r.p.res) apex:poja)]
    %auth
      :_  +>.$
      [ost %give %nice ~]~
    ?(%get-id %send %buy %list %sell)
      ~&  [`@tas`-.pax res]
      :_  +>.$
      [ost %give %nice ~]~
    %sent
      =+  resp=(rash q:(need r.p.res) apex:poja)
      =+  suc=((ot success/so ~):jo resp)
      :_  +>.$
      :_  [ost %give %nice ~]~
      ?~  suc  
        (publish ost our.hid [%lin & (crip "something went wrong")]~)
      (publish ost our.hid [%lin & (crip "success!")]~)
  ==
::
++  publish
  |=  [ost=bone you=ship act=(list speech)]
  ^-  move
  =+  ^=  spchz
      %+  turn  act
      |=  sp=speech
      =+  ^=  tail
      :-  ^-  audience
          :+  :-  `partner`[%& our.hid ?+((clan our.hid) !! %czar %court, %duke %porch)]
              ^-  (pair envelope delivery)
              [`envelope`[& ~] %pending]
            ~
          ~
      `statement`[lat.hid ~ sp]
      ^-  thought
      :-  `@`(sham eny.hid tail)
      tail
  =+  cag=`cage`[%talk-command !>([%publish `(list thought)`spchz])]
  [ost %pass /auth %g %mess [our.hid /talk] you cag]
--
