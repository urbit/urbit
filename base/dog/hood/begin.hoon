::
::::  /hook/gate/begin/hood/gun
  ::
/?  314
/-  *sole
::
::::
  !:
=>  |%
    ++  begs  ,[his=@p tic=@p yen=@t ges=gens]
    --
|=  $:  [now=@da eny=@uvI bec=beak]
        [~ ~]
    ==
^-  (sole-result (cask begs))
%+  sole-lo
  [%& %hood-begin "your ship: ~"]
%+  sole-go  fed:ag
|=  his=@p
%+  sole-lo
  [%& %hood-ticket "your ticket: ~"]
%+  sole-go  fed:ag
|=  tic=@p
%+  sole-lo  
  [%& %hood-entropy "some entropy: "]
%+  sole-go  (boss 256 (more gon qit))
|=  yen=@t
=+  ney=(shax yen)
%+  sole-yo  `tank`[%leaf "entropy check: {(scow %p `@p`(mug ney))}"]
%+  sole-so  %hood-begin
:*  his
    tic
    ney
::
    ^-  gens
    :-  %en
    =+  can=(clan his)
    ?-  can
      %czar  [%czar ~]
      %duke  [%duke %anon ~]
      %earl  [%earl (scot %p his)]
      %king  [%king ?:(=(~doznec his) 'Urban Republic' (scot %p his))]
      %pawn  [%pawn ~]
    ==
==
