::                                                      ::  ::
::::  /hook/core/hood/ape                               ::  ::
  ::                                                    ::  ::
/?  314                                                 ::  zuse version
/+  sole, talk, helm, kiln                              ::  libraries
::                                                      ::  ::
::::                                                    ::  ::
  !:                                                    ::  ::
=>  |%                                                  ::  module boilerplate
    ++  hood-good                                       ::
      |*  hed=hood-head                                 ::
      |=  paw=hood-part                                 ::
      ?-  hed                                           ::
        %helm  ?>(?=(%helm -.paw) `helm-part`paw)       ::
        %kiln  ?>(?=(%kiln -.paw) `kiln-part`paw)       ::
      ==                                                ::
    ++  hood-head  ,_-:*hood-part                       ::
    ++  hood-make                                       ::
      |*  hed=hood-head                                 ::
      ?-  hed                                           ::
        %helm  *helm-part                               ::
        %kiln  *kiln-part                               ::
      ==                                                ::
    ++  hood-part                                       ::
      $?  helm-part                                     ::
          kiln-part                                     ::
      ==                                                ::
    --                                                  ::
::                                                      ::  ::
::::                                                    ::  ::
  ::                                                    ::  ::
|_  $:  hid=hide                                        ::  system state
        [%0 lac=(map ,@tas hood-part)]                  ::  part system
    ==                                                  ::
++  able                                                ::  find/make part
  |*  hed=hood-head
  =+  rep=(~(get by lac) hed)
  =+  par=?^(rep u.rep `hood-part`(hood-make hed))
  ((hood-good hed) par)
::
++  ably                                                ::  save part
  |*  [moz=(list) rep=hood-part]
  [(flop moz) +>(lac (~(put by lac) -.rep rep))]
::                                                      ::  ::
::::                                                    ::  ::
  ::                                                    ::  ::
++  poke-hood-begin                                     ::
  |=  [from hood-begin]
  (ably (poke-begin:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-helm-init                                      ::
  |=  [from hood-init]
  (ably (poke-init:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-hood-reload                                    ::
  |=  [from hood-reload]
  (ably (poke-reload:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-hood-reset                                     ::
  |=  [from ~]
  (ably (poke-reset:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-hood-verb                                      ::
  |=  [from ~]
  (ably (poke-verb:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-hood-start                                     ::
  |=  [from hood-start]
  (ably (poke-start:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-will                                           ::
  |=  [from (unit will)]
  (ably (poke-will:(helm-work [hid +<-] (able %helm)) +<+))
::
++  poke-hood-merge                                     ::
  |=  [from hood-merge]
  (ably (poke-merge:(kiln-work [hid +<-] (able %kiln)) +<+))
::
++  poke-hood-sync                                      ::
  |=  [from hood-sync]
  (ably (poke-sync:(kiln-work [hid +<-] (able %kiln)) +<+))
::
++  poke-hood-unix                                      ::
  |=  [from hood-unix]
  (ably (poke-unix:(kiln-work [hid +<-] (able %kiln)) +<+))
::
++  onto-helm                                           ::
  |=  [then saw=(each ,[term @tas @da] tang)]
  (ably (take-onto:(helm-work [hid ost src] (able %helm)) +<+))
::
++  mere-kiln                                           ::
  |=  [then are=(each (set path) (pair term tang))]
  (ably (take-mere:(kiln-work [hid ost src] (able %kiln)) way +<+))
::
++  made-kiln                                           ::
  |=  [then @uvH (each gage tang)]
  (ably (take-made:(kiln-work [hid ost src] (able %kiln)) way +<+))
::
++  note-helm                                           ::  
  |=  [then (pair ,@tD tank)]
  (ably (take-note:(helm-work [hid ost src] (able %helm)) way +<+))
::
++  coup-kiln-fancy                                     ::
  |=  [then saw=(unit tang)]
  (ably (take-coup-fancy:(kiln-work [hid ost src] (able %kiln)) way +<+))
::
++  coup-kiln-spam                                      ::
  |=  [then saw=(unit tang)]
  ~?  ?=(^ saw)  [%kiln-spam-lame u.saw]
  [~ +>] 
::
++  went-helm                                           ::
  |=  [then her=ship kay=cape]
  (ably (take-went:(helm-work [hid ost src] (able %helm)) way +<+))
--
