::    A simple Twitter proxy.
::
::::  /hook/core/twit/app
  ::
/?    314                                               ::  need urbit 314
/+    twitter                                           ::  use twitter library
::
::::  structures
  ::
|%                                                      ::  structures
  ++  axle  ,@t                                         ::  application state
  ++  gilt                                              ::  subscription frame
    $%  [%json p=json]                                  ::  json data
        [%html p=@t]                                    ::  html text 
        [%hymn p=manx]                                  ::  html tree
    ==                                                  ::
  ++  gift                                              ::  output action
    $%  [%rust gilt]                                    ::  complete update
        [%rush gilt]                                    ::  incremental update?
        [%mean (unit (pair term (list tank)))]          ::  terminate
        [%nice ~]                                       ::  succeed
    ==                                                  ::
  ++  move  ,[p=bone q=(mold note gift)]                ::  output operation
  ++  sign                                              ::  system response
    $%  $:  %e                                          ::  from %eyre
    $%  [%thou p=httr]                                  ::  HTTP response
    ==  ==  ==                                          ::
  ++  note                                              ::  system request
    $%  $:  %e                                          ::  through %eyre
    $%  [%them p=(unit hiss)]                           ::  HTTP request
    ==  ==  ==                                          ::
--                                                      ::
::
::::  secrets!
  ::
|%
++  app-key                                             ::  hardcoded keys!
  :*  :-  'hDDOTPfGHGlsOUbhpy6qc6XbW'
          'olCkea6wm3XG4pnVCHuPIozUF2ggH1sHjnBtuT4Ai6rCOeQGzO'
      :-  '2485712317-R77Lpdu5rAJadRVxTXPpnxvcwS0IfNG7QEzLPty'
          'a41d83XId0P7QQbodkPYv3zxoEL0Cq9EsN2eXZBZAwAWA'
  ==
--
!:
::::  program
  ::
|_  $:  hid=hide                                        ::  standard state
        vat=axle                                        ::  custom state
    ==
++  prep                                                ::  load old state
  |=  old=(unit)
  ~&  %hose-state
  [~ +>]
::
++  root                                                ::  internal location 
  /(scot %p our.hid)/main/(scot %da lat.hid)/app/[app.hid]
::
++  incl                                                ::  include scripts
  |=  wal=wall
  %+  turn  wal
  |=  tape  ;script(type "text/javascript", src +<);
:: 
++  peer                                                ::  accept subscriber
  |=  [ost=bone you=ship pax=path]
  ^-  [(list move) _+>]
  :_  +>.$
  ~&  [%peer ost pax]
  ?~  pax
    [ost %give %rust %hymn page]~
  ?>(?=([%time ~] pax) ~)
:: 
++  poke-json                                           ::  client message
  |=  [ost=bone his=ship jon=json]
  ^-  [(list move) _+>]
  :_  +>.$
  ~&  [%got-json ost]
  =+  jop=(need ((of [%tweet sa] [%time sa] ~):jo jon))
  ~&  [%got-jop ost]
  ?-    -.jop
      %tweet  
    ~&  [%poke-tweet-path /tweet/(scot %ud ost)]

    :~  [ost %pass /tweet/(scot %ud ost) %e [%them (some (posl jon))]]
        [ost %give %nice ~]
    ==
  ::
      %time
    ~&  %poke-time
    [ost %give %nice ~]~
  ==
::
++  page                                                ::  build front page
  ^-  manx
  ;html
    ;head
      ;style:"{(trip ;;(,@ .^(%cx (welp root /main/css))))}"
      ;*  %-  incl  :~
        "//cdnjs.cloudflare.com/ajax/libs/jquery/2.1.1/jquery.min.js"
      ==
      ;title: Urbit - Twitter Test
    ==
    ;body
      ;div#c
        ;div#d
          ;div#twet
            ;div.author: @urbit_test
            ;div.date;
            ;textarea#tweetr(placeholder "What would you like to tweet?");
            ;input#submit(type "button", value "Send");
            ;input#length(type "button", value "0/140");
          ==
          ;div#time;
        ==
      ==
      ;script:"{(trip ;;(,@ .^(cx/(welp (scag 3 `path`root) /lib/urb/js))))}"
      ;script:"{(trip ;;(,@ .^(%cx (welp root /main/js))))}"
    ==
  ==
:: 
++  pour-time                                           ::  timeline response
  |=  [pax=path sih=sign]
  ^-  [(list move) _+>]
  ~&  %pour-time
  :_  +>.$
  =.  vat  `@t`q:(need r.p.sih)
  %+  turn
    %+  skim  (~(tap by sup.hid)) 
    |=  [ost=bone his=ship pax=path]
    ~&  [%pour-time-pax pax]
    ?=([%time ~] pax)
  |=  [ost=bone his=ship pax=path]
  ~&  [%pour-time-give ost his pax]
  =-  [ost give/rush/json/-]
  (rash vat apex:poja)
::
++  pour-tweet                                          ::  tweet response
  |=  [pax=path sih=sign]
  ^-  [(list move) _+>]
  ~&  %pour-tweet
  =+  ost=(slav %ud -.+.pax)
  :_  +>.$
  :~  [ost %give %nice ~]
      [ost %pass /time %e [%them (some timl)]]
  ==
::
++  pour                                                ::  API response
  |=  [pax=path sih=sign]
  ^-  [(list move) _+>]
  ~&  [%pour pax]
  ?:  ?=([%time ~] pax)
    (pour-time pax sih)
  ?>  ?=([%tweet *] pax)
  (pour-tweet pax sih)
::
++  timl                                                ::  timeline request
  ^-  hiss
  ~&  %request-timeline
  =+  (twit `keys`app-key lat.hid `@`eny.hid)
  (stat-home ~ ~)
::
++  posl                                                ::  post request
  |=  jon=json
  ~&  [%posl jon]
  =+  txt=`(unit ,[%tweet p=cord])`((of:jo [%tweet so:jo] ~) jon)
  ~&  [%posl-txt txt]
  ^-  hiss
  =+  (twit `keys`app-key lat.hid `@`eny.hid)
  (stat-upda ~[(st ~ p:(need txt))] ~)
--
