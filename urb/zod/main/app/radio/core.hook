/-  mess,user,users,zing,zong
!:
=>  |%
    ++  axle
      $%  [%0 p=(map path ,[p=(list zong) q=(map ship ,?)])]
      ==
    ++  blitz
      $%  [%zong p=zong]
          [%user p=user]
      ==
    ++  iron
      $%  [%zongs p=(list zong)]
          [%users p=users]
      ==
    ++  gift
      $%  [%rush blitz]
          [%rust iron]
          [%mean ares]
          [%nice ~]
      ==
    ++  move  ,[p=bone q=(mold note gift)]
    ++  note  ,~
    --
|_  [hid=hide vat=axle]
++  grab
  |=  sta=path
  (fall (~(get by p.vat) sta) *[p=(list zong) q=(map ship ,?)])
::
++  ident
  |=  you=ship
  %-  (hard ,@t)
  .^(%a (scot %p our.hid) %name (scot %da lat.hid) (scot %p you) ~)
::
++  peer
  |=  [ost=bone you=ship pax=path]
  ^-  [(list move) _+>]
  ?~  pax
    [[ost %give %mean ~ %radio-bad-path ~]~ +>.$]
  =+  ya=(grab t.pax)
  ?+    -.pax  [~ +>.$]
      %mensajes
    [[ost %give %rust %zongs p.ya]~ +>.$]
      %amigos
    =.  q.ya  (~(put by q.ya) you %.y)
    :_  +>.$(p.vat (~(put by p.vat) t.pax ya))
    :_  (send pax %give %rush %user %in you (ident you))
    :*  ost  %give  %rust  %users
        %+  murn  (~(tap by q.ya))
        |=  [shi=ship liv=?]
        ?.  liv
          ~
        (some [shi (ident shi)])
    ==
  ==
::
++  poke-zing
  |=  [ost=bone you=ship zig=zing]
  ^-  [(list move) _+>]
  =+  ya=(grab p.zig)
  ?.  (~(has by q.ya) you)
    [[ost %give %mean ~ %no-te-conozco ~]~ +>.$]
  =+  zog=`zong`[%mess lat.hid you q.zig]
  =.  p.vat  (~(put by p.vat) p.zig [[zog p.ya] q.ya])
  :_  +>.$
  :-  [ost %give %nice ~]
  (send mensajes/p.zig %give %rush %zong zog)
::
++  poke-bit
  |=  [ost=bone you=ship ~]
  ^-  [(list move) _+>]
  :_  +>.$
  %+  turn  (~(tap by sup.hid))
  |=  [ost=bone her=ship pax=path]
  [ost %give %mean ~ %reload leaf/"please quit chat and re-enter" ~]
::
++  pull
  |=  ost=bone
  ^-  [(list move) _+>]
  =+  ^-  (unit ,[his=ship pax=path])
      (~(get by sup.hid) ost)
  ?~  -  ~&  %strange-pull  [~ +>.$]
  ?.  ?=([%mensajes *] pax.u)
    [~ +>.$]
  =+  sta=(~(got by p.vat) t.pax.u)
  =.  q.sta  %+  ~(put by q.sta)  his.u  %.n
  =.  p.vat  %+  ~(put by p.vat)  t.pax.u  sta
  [(send amigos/t.pax.u %give %rush %user %out his.u (ident his.u)) +>.$]
::
++  send
  |=  [pax=path msg=(mold note gift)]
  ^-  (list move)
  ::  ~&  [%radi-sub pus.hid]
  %+  turn  (~(tap in (~(get ju pus.hid) pax)))
  |=(ost=bone [ost msg])
--
