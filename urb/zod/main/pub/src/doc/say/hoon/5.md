---
layout: tutpage
axis: doc-hoon
categories: tut
sort: 5
title: V - Tiles
---

> Not to get knowledge, but to save yourself from having
> ignorance foisted upon you.
> - Carlyle

##Tiles##

We actually told a little white lie when we said that a Hoon file
turns into a twig.  Well, sort of.  It does turn into a twig.
But parts of it often start out as something else - a `tile`.

What is a tile?  First, concretely, a tile is an AST subtree
that's reduced statically into a twig.  It can be reduced in four
ways - cryptically called `bunt`, `clam`, `fish`, and `whip`.  A
tile is always some leg of a twig, and that twig defines how the
tile is reduced.  (Don't worry, we'll see plenty of examples.)

Abstractly, though, when we see tiles in Hoon peer at them
through our rapidly fading 20th-century programming spectacles,
what we see looks a heck of a lot like a *type declaration*.  

But no.  Just as a gate is not a lambda, a tile is not a type.
And worse - we've already used the word `type` for something
else.  And worst of all - each tile corresponds to a well-defined
type, called its *icon*.  But tile and icon are not in any way
the same noun.  You can see the potential confusion...

A tile is actually two steps away from being a type, in the
strict sense of `++type`.  The tile is converted statically
into a twig, which in turn may (depending on the conversion)
produce the icon, test for it, etc.  And always, the icon is 
some function of the tile and its subject.

Nonetheless, even the most experienced Hoon programmers can be
heard saying "type" when they mean "tile."  Apparently you can
take the boy out of the '90s but not the '90s out of the boy.
In fact, it's tempting to suggest that if there's one problem
with 20th-century programming in general, it's that it doesn't
know the difference between a tile and its icon.

Imagine, for instance, a C++ in which the class definition and
the constructor are the same thing.  And the latter can also
validate untrusted data off the Internets, acting much as for
instance an XML DTD.  Why would you have a type system and
not be able to do this?

##Reductions##

Broadly speaking, a tile is a *convenient way of making a
well-typed noun*.  

It's important to note that tiles are really pure sugar.  It is
possible to build a language like Hoon, but with only twigs and
no tiles.  However, certain common tasks in this language will
always be unnecessarily cumbersome.  

A tile is not a type declaration, but it has roughly the same job
to do - and this is by no means a trivial job.  So tiles aren't
trivial.  Sorry, we wish they were.  (Actually we wish they
didn't exist at all, but couldn't figure out how to swing it.)

We use tiles by converting them to twigs, which produce or
identify nouns of a type easily predictable from the tile.
Again, there are four such reductions: `bunt`, `clam`, `fish`, 
and `whip`.

###Bunting###

Bunting a tile - if `sec` is a tile, `~(bunt al sec)` produces
its bunt - makes a twig that *creates a blank default example*
of the tile's icon. 

Bunting is actually the most common use of a tile.  Consider the
gates we've just been defining - how do we build a gate?   With

    ++  deq
      =+  x=0
      |.  
      =+  y=0
      |-  
      ?:  =(x +(y))
        y
      $(y +(y))

The value `x=0` is a useless default.  It's in our gate only
because the gate is built first, then modified with the real
sample.  Accordingly, in practice to build a gate we use the
synthetic hoon `|=` (`bartis`, `%brts`), which is defined as

    ++  twig  $%  [%brts p=tile q=twig]
              ==

and 

    ++  open
      ^-  twig
      ?-    gen
        [%bctr *]  [%ktsg ~(bunt al p.gen)]
        [%brcb *]  [%tsls [%bctr p.gen] [%brcn q.gen]]
        [%brts *]  [%brcb p.gen (~(put by *(map term foot)) %$ [%ash q.gen])]
      ==

(The only hoon we haven't met here is `^~` (`ketsig`, `%ktsg`),
which tells the compiler to compute a constant expression at
compile time.  Obviously, the default sample in a gate should
under almost every circumstance be a constant.)
  
With `|=` we have:

    ++  deq
      |=  x=@
      =+  y=0
      |-  
      ?:  =(x +(y))
        y
      $(y +(y))

or if we prefer

    ++  deq  |=(x=@ =+(y=0 |-(?:(=(x +(y)) y $(y +(y))))))

What is `x=@`?  A tile.  And the tile in `|=` is always bunted to
create the default sample.  

Furthermore, it's wrong to say that the default sample is
useless.  It generates a corresponding *default product*.  It is
perfectly legitimate to simply apply a gate, even a previously
unviolated gate, without changing the sample at all.

For example, one type of tile is simply a gate.  When we bunt
such a gate, we have no choice but to produce the default
product.  Thus any gate arm that wants to be used as a tile
has the responsibility to produce a useful default product.

###Clamming###

The `clam` reduction generates a gate which accepts an arbitrary
noun and produces a member of the tile's icon.  Ie, `clam`
generates a validator function for the icon.  Perhaps you
can see how this might be useful, at least, on the Internets.

Moreover, a clam for any tile, if passed a member of the type, is
guaranteed to produce its sample.  If passed a noun outside
the type, it will normalize and/or default.  It won't crash -
except for the custom cases of `%herb` and `%weed`.

###Fishing###

The `fish` reduction lets us test if a noun matches the tile,
with the `?=` (`wuttis`, `%wtts`) natural hoon:

    ++  twig  $%  [%wtts p=tile q=wing]
              ==

Note that `?=` only works on a wing and more specifically leg -
that is, it has to be testing a fragment of the subject.

Moreover - we'll discuss this in more detail when we get to type
inference - when we use `?=` in a `?:` test, the compiler sees
the test and uses it to refine the subject type on either side
of the branch.  Hence:

    ?:  ?=(@ a)             ::  test if a is an atom
      XX                    ::  code assuming a is an atom
    XX                      ::  code assuming a is a cell

Some languages call fishing "pattern matching." 

###Whipping###

Besides fishing on a leg, we can normalize any leg to any tile
using the `$@` (`bucpat`, `%bcpt`) hoon - aka, `whip`.  Not only
is `whip` the same thing as `clam`, it's what `clam` uses
internally. All `clam` does is make a gate whose sample, an 
arbitrary noun `+<` (`glus`, `.6`), it whips into its tile.

##Tile syntax and abuse##

A wide variety of twigs contain tiles, but the basic tiling twigs
are worth listing in one place, along with their regular and
irregular syntax:

    ++  twig  $%  [%bccb p=tile]
                  [%bccm p=tile]
                  [%bcpt p=wing q=tile]
                  [%bctr p=tile]
                  [%wtts p=tile q=wing]
              ==

`$_` (`buccab`, `%bccb`) bunts tile `p`.

`$*` (`buctar`, `%bctr`) bunts tile `p` and folds it statically
to a constant with `%ktsg`.

`$,` (`buccom`, `%bccm`) makes a clam for tile `p`.

`$@` (`bucpat`, `%bcpt`) whips the leg at wing `p`, filtering it
with tile `q`.

`?=` (`wuttis`, `%wtts`) fishes the leg at wing `q` for tile `p`,
producing & (yes) if and only if leg `q` is in tile `p`.

(`%wtts` looks different because it produces a loobean, not a
noun in the tile.)

One thing we'll see quite frequently is the irregular wide forms
of these hoons (in fact, the regular forms of many are rare).
Consider the base tiles `*` and `@` - ie, noun and atom.  (And
note that tiles, too, have tall, wide, and irregular wide forms.)

The `_` prefix before a wide tile is `%bccb`:

    ~zod/try=> _*
    0

    ~zod/try=> :type; _*
    0
    *

The `*` prefix on a tile is `%bctr`, the same thing folded to a
constant (obviously a no-op here)

    ~zod/try=> **
    0
    ~zod/try=> :type; **
    0
    *

The `,` prefix on a tile is `%bccm`, ie, generate a function
producing the tile.  Let's save one of our nouns and make it an
atom, using a clam.  Let's try the clam on different things:

    ~zod/try=> =foo _*
    ~zod/try=> (,@ foo)
    0
    ~zod/try=> :type; (,@ foo)
    0
    @
    ~zod/try=> :type; (,@ [5 5])
    0
    @
    ~zod/try=> :type; (,@ %foo)
    7.303.014
    @

Alternately, we could check if our `*` is a `@` with `?=`:

    ~zod/try=> ?=(@ foo)
    %.y

Why is all this true?  Because the bunt of `*` is the atom `0`
with the type `%noun`.
 
Finally, if a tile appears where we expect a twig, the normal
tile-to-twig conversion is always `%bccm`.  Ie, using a tile as a
twig generates a gate producing the icon.

##The tiles##

With a good general understanding of what a tile is for - we'll
go back into the tiles we actually have:

    ++  tile  
      $&  [p=tile q=tile]                     ::  ordered pair
      $%  [%axil p=base]                      ::  base type
          [%bark p=term q=tile]               ::  name
          [%bush p=tile q=tile]               ::  pair/tag
          [%fern p=[i=tile t=(list tile)]]    ::  plain selection
          [%herb p=twig]                      ::  function
          [%kelp p=[i=line t=(list line)]]    ::  tag selection
          [%leaf p=term q=@]                  ::  constant atom
          [%reed p=tile q=tile]               ::  atom/cell
          [%weed p=twig]                      ::  example
      ==                                      ::
    ++  line  ,[p=[%leaf p=odor q=@] q=tile]
    ++  base  ?([%atom p=odor] %noun %cell %bean %null)

Observe that the tile stems are no longer rune digraphs.  Rather,
a gardening metaphor appears to pertain.  Let's work through
these one by one, showing both (regular) syntax and semantics.

###`[%axil p=base]`###

An `%axil` is a simple built-in mechanism for a few basic
icons: an atom of any odor (`@odor`, or just `@` for the
odorless base atom); a noun (`*`); a cell of nouns (`^`); a
loobean `?`; and null `~`. 

###`[%leaf p=term q=@]`###

A `%leaf` is an atomic constant of value `q` and odor `p`.
Obviously its icon is a `%cube`.

The syntax for a leaf is the same as the twig syntax, except that
`%` is never required to generate a cube.  For instance, as a
twig, `7` has a type of [%atom %ud]; `%7` has a type of 
`[%cube 7 [%atom %ud]]`.  But the icon of the leaf `7` is,
again, `[%cube 7 [%atom %ud]]`.

###`[p=tile q=tile]`###

Tiles autocons, just like twigs - a cell of tiles is a tile of
a cell.  But we shouldn't skip the differences too lightly.

A cell of twigs is also a twig of a cell.  But in the cell of
twigs, the subject of either leg is the original subject.
Whereas when applying a tile in a whip or clam, the subject is
split in half for equally obvious reasons.  

(Otherwise, how would `(,[@ @] [4 5])` be `[4 5]`?  Clearly, this
should be the same as `[(,@ 4) (,@ 5)]`.  Clearly, it should not
be the same as `[(,@ [4 5]) (,@ [4 5])]` - which would produce
merely `[0 0]`.)

The irregular wide syntax for tile autocons is the same as the
syntax for twig autocons - eg, `[@ @]`, a cell of atoms.  But
there is also a regular tall/wide tuple syntax, with `$:`
(`buccol`, `%bccl`).  Thus instead of `[@ @]` we could write:

    $:  @
        @
    ==

###`[%bark p=term q=tile]`###

Wrap a name round a tile.  `a=*` parses as `[%bark %a %noun]`.

This is another case where the tile syntax matches the twig
syntax, but only in the irregular form.  The twig equivalent of
`%bark` is of course `^=` (`kettis`, `%ktts`).  But the tile is
`$=` (`buctis`):

    $=  a
    *

Obviously a silly syntactic arrangement.  But you can need it if
`q` is really big.

###`[%reed p=tile q=tile]`###

A `%reed` is a tile whose icon contains two kinds of nouns: atoms
of tile `p` and cells of tile `q`.

There is no irregular form of `%reed`.  The regular form is:

    $|  ~
        [@ @]
    ==

or in wide mode, of course, `$|(~ [@ @])`.

###`[%kelp p=[i=line t=(list line)]]`###

A `kelp` is the workhorse of tiles - it provides the most common
data structure in any language, the discriminated union.  

In Hoon, the head (which must be a `leaf`) is called the `stem`.
The tail (which can be anything) is the `bulb`.  Cases of a kelp
are known inevitably as `fronds`.  

(Yes.  We're aware that "kelp" is not properly a singular noun.
In Hoon - it is properly a singular noun.  And that's that.  And 
oddly, it's not that hard to run out of four-letter plants.)

###`[%bush p=tile q=tile]`###

A `%bush` is a tile in which there are two kinds of nouns: cells
whose head is a cell (tile `p`) and cells whose head is an atom
(tile `q`).  Its default value is the value of `q`.

We don't have to look very far to find a bush - `++tile` is one,
as is `++twig` and `++nock`.  The rune is `$&` (`bucpam`).  See
`++tile` above - `p` is `[p=tile q=tile]`, `q` is the `$%`.
There is no irregular form.

What's the use of a bush?  Often in a variety of data structures
we have something like autocons, in which forming a cell of two
instances has an obvious default semantics.  

Sure, we could attach these semantics to an atom, and just use a
`%kelp`.  In twigs, tiles, or nock formulas, we could have an
explicit *cons* stem of some sort.  But it would be a bulky and
annoying pain in the butt as compared to autocons.

###`[%fern p=[i=tile t=(list tile)]]`###

A `fern` is a non-empty list of cases; its icon is naturally
a `%fork`.  The programmer is responsible for ensuring that the
cases are actually orthogonal (unlike with the structured forks,
`%bush`, `%kelp` and `%reed`).  A good general practice is to use
ferns only with leaves (see below).

For example, a fern that could be `%foo` or `%bar` has the
irregular form `?(%foo %bar)`, or the regular form

    $?  %foo
        %bar
    ==

The default value is the first - in this case, `%foo`.

But wait. Why do we need these other selection tiles, `%bush` and
`%reed` and `%kelp`, if we have `%fern`?

The answer is that tiles aren't magic.  They are really just ways
of generating specific twigs that do useful things with nouns.
Suppose, for instance, you use a `%fern` as a `%kelp`.  You can
do this - but we may well generate more effective validation code
for a `%kelp`, whose structure we understand how to search for.

And even if were this not the case, anyone creating complex tiles
and icons should use one of these standard structures - simply
because it is never really worth varying from this set of
patterns, even if small structural efficiencies can be extracted.
Therefore, it's worth telling both the compiler and the reader
exactly what the programmer is trying to do in each case.

###`[%herb p=twig]`###

You can write your own tile which is just a gate, accepting a
sample of `*` and normalizing it as you choose.  If you use a
twig as a tile, it's treated as an herb.

For example, when we define a gate like `++base`, as defined
above (remember that when we use a tile as a twig, we get the
clam, ie, the normalizing gate) `base` is just an arm which
produces a gate.  Nothing has any idea that this gate is built
from a tile of its own.

So when we parse `[p=base q=base]` as a tile, the parser builds
the noun:

    [[%bark %p %herb %cnzy %base] [%bark %q %herb %cnzy %base]]

In other words, `base` in `p=base` is actually a *twig*, but this
twig happens to produce a normalizing gate generated by clamming
a tile.  In time this will come to seem totally straightforward,
but don't be surprised if it confuses you now.

The important thing to remember about `%herb` is that the actual
twig we provide will be applied when we whip or clam.  Hence,
arbitrary normalization and/or verification procedures may be
part of the herbaceous custom tile.

###`[%weed p=twig]`###

`%weed` is the lamest kind of tile - tile by example.  A `%weed`
is defined by its example twig `p`, which produces the icon.

One limitation is obvious - it is essentially useless to whip or
clam for a `%weed`.  Whipping or clamming on a `%weed` simply
ignores the sample and reproduces the example - as does bunting,
of course.  However, `%weed` remains quite useful.

The irregular syntax for a `%weed` is a `_` prefix - for example,
to use a twig `foo` as a `%weed`, we write `_foo`.  The regular
form is `$_` (`buccab`):

    $_  foo
