---
layout: tutpage
axis: doc-hoon
categories: tut
sort: 4
title: IV - Gates
---

> Anyone who thinks he's original is merely ignorant.
> - Nicolás Gómez Dávila

It's nice that we got decrement working in the last chapter.  But
we didn't actually produce a decrement *function*.  So, let's do
that now.

##Gates and "lambda"##

While we can hardly stop you from saying "function," the proper
Urbit terminology is `gate`.  

A gate is a kind of core - you might remember our discussion in
chapter 4.  A core is

    [battery payload]

The battery is one or more formulas; the payload is any noun.  

In a gate, there is exactly one arm - whose name is `$`, `buc` or
`blip` - ie, nothing.  One arm, one twig, one formula.  Moreover,
the payload is always a cell, arbitrarily labeled `sample` and
`context`.  

So - to combine several ways of deconstructing a noun - a gate
looks like this:

               glus   gras
              lusgal lusgar
                +<     +>
    [formula [sample context]]
        -          +
       hep        lus

Does this remind you of anything?  I hope not!  Unfortunately
some readers may be previously familiar with legacy functional
languages from the late 20th century.  Legacy?  Perhaps this is a
little sharp - let's say "classical."

In classical FP, something quite like a Hoon gate is known as a
"lambda." We try not to use this term, really.  Hoon is obviously
not based on the lambda calculus, and it's not nice to define
other people's words for them.  (This is also why we don't say
"monad.")  But we may slip up sometimes and that's okay.  (We may
also get caught with "closure" or even "continuation.") 

But there is a big difference: in classical FP, "lambda" is a
fundamental semantic primitive.  Since these are *functional*
languages after all, it makes sense that defining and calling
functions would be fundamental primitives.  

But gates - Hoon's equivalent of functions - are no more than a
design pattern: a convention, and a relatively funky convention
at that.  Plus some sugary macros that make things convenient.

Since Hoon's primitives are actually primitive (it doesn't get
much more primitive than Nock), we can combine them to produce
not only classic, function-oriented FP, but also a broader garden
of funky patterns at the same complexity level as function
calling.  But different from function calling.  What?  We'll see
some of these patterns in a bit.

###We make a gate###

Our goal this time is not just to build decrement - it's to build
decrement *right*.  That is, as a decrement function - a gate.
If this is called `deq` (there already being a perfectly usable 
`dec`) and we want decrementing `a` to look like Lisp:

    (deq a)

then we can expect our program to look something like 

    !:             ::  To write a trivial Hoon program
    |=  *          ::
    |=  [a=@ ~]    ::  For educational purposes only
    :_  ~  :_  ~   ::
    :-  %$         ::  Preserve this mysterious boilerplate square
    !>             ::
    :::::::::::::::::  Produce a value below
    =>  XXXXX      ::  some twig or other
    (deq a)

This XXXXX twig, whatever it is, must extend the subject to
contain a core which includes an arm which produces our gate.

Extend the subject to contain a core which includes an arm which
produces our gate?  Sure:

```
=>  |%
    ++  deq
      =+  x=0
      |.  =+  y=0
          |-  ?:  =(x +(y))
                y
              $(y +(y))
    --
(deq a)
```
Or more conveniently:

```
=<  (deq a)
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```
Does this work?

    : /~zod/try/25/bin/hec/hoon
    ~zod/try=> :hec 42
    41

But this doesn't mean we understand it.  

Sure, we read the last chapter.  So we understand how and why
`|%` turned into `|.`, which then turned into `|-`.  The loop.
But this strange beast seems to contain... *three* cores? 

###We understand a gate##

Of course we need three nested cores.  Actually, we have a lot
more than three nested cores, because the subject we are getting
for the application contains all of `zuse.hoon` and `hoon.hoon`.

A stack of cores in which the core below is the context of the
one above, all the way down to the innermost core whose payload
is a simple constant (for `hoon.hoon`, always the Kelvin version)
is called, for obvious reasons, a `reef`.  With our three cores,
we are adding no more than a few decorative polyps to our reef.

For example, we can just as well use the standard decrement,
`dec` - it's in there too:

```
=<  (dec a)
|%
++  deq
  =+  x=0
  |.  
  =+  y=0
  |-  
  ?:  =(x +(y))
    y
  $(y +(y))
--
```
(We've used a more conservative indentation here - uglier and
less compact, but arguably clearer.)

We define `deq`, but we seem to be able to just call `dec`.  Why?
Because we read past cores which don't bind the arm we're looking
for, we can read all the way to the deepest layer in the Hoon 164
reef - which contains the official, original `dec`.

(It remains important not to profoundly abuse this magic power.
Every layer of core you add, of course, puts you farther from
some of the gates you want to use.  A smart interpreter can
smooth over this issue a little, but smart isn't free either.)

Thus, it should be perfectly clear what the three `bar` runes -
implying three cores - mean.  First, we have the `|%` which
contains `++deq`.  (When referring to an arm in informal text,
we use this syntax, though the actual language of course requires
a double space - when searching in a file, <code>++&ensp;&ensp;arm</code> will always
find the definition of `arm` and nothing else.)

This outer core defines *the library our decrement gate is in*.
Actually, "library" (or `book` - defined as a core which
contains only code and constant data) is not quite the right
terminology here.  Even though we don't use it and it shouldn't
in fact be there, the payload of this core contains dynamic
information such as `a`.

In actual fact our nascent integer math library  is really an
"object" or `cart` - ie, a core containing dynamic state.  This
is just wrong and we'll see later on how to do it right.

As in every core, the subject of every arm is the core itself.
Naturally, the `|%` twig uses its own subject as the payload,
creating the familiar reef effect as the stack of cores piles up.

And this library is the `context` (or `+>`, `gras`) of our gate.
Thus, the twig in the gate can use anything in the library - of
course as needed.

The `sample` (or `+<`, `glus`) of our gate is simply any default
noun for the type we want to compute on.  Remember, of course,
that the whole point of gates is that we change the sample, then
compute the formula.  Hoon is a typed language, so the sample can
be safely replaced by any noun in the same type.

Within `=+`, `tislus`, the old subject becomes the context, and
the new subject adds the sample `^=(b 0)`.  This pair of sample
and context then becomes the payload within `|.`  Hence, we build
a gate.

And then, within this gate - the third core is the decrement loop
itself, as we built it in the last chapter.  Note that if
decrement did not need a counter variable or other incremental
state, we would need only two cores - we could recurse through
the decrement gate itself.  We'll see this in a little bit.

Let's eschew synthetic runes and show the three cores as they
really are:

```
=<  (deq a)
|%
++  deq
  =+  x=0
  |%  
  ++  $
    =+  y=0
    =<  $
    |%  
    ++  $
      ?:  =(x +(y))
        y
      $(y +(y))
    --
  --
--
```

On the other hand, if we don't mind synthetics and lots of
parentheses, our decrement gate can also be a one-liner.  This is
perfectly legitimate Hoon style:

```
|%  ++  deq  =+(x=0 |.(=+(y=0 |-(?:(=(x +(y)) y $(y +(y)))))))
--
```

###We call a gate###

Note that we've still totally failed to explain `(deq a)`.  This
is obviously an irregular form.  Let's try to turn it back into
natural hoons, and figure out what it's doing.
 
We know that `deq` builds a gate.  We already know how to change
the sample, `+<` or `glus`, in that gate.  So let's drag it out 
and do it the ugly way:

```
=<  =+  foo=deq             ::  create the gate
    =+  bar=foo(+< a)       ::  replace its sample
    $.bar                   ::  invoke the gate
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```

Does this work?  It's always nice to check...

    : /~zod/try/28/bin/hec/hoon
    ~zod/try=> :hec 42
    41

We don't actually need a separate `bar`:

```
=<  =+  foo=deq             ::  create the gate
    $.foo(+< a)             ::  replace the sample and invoke
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```

But what we'd actually like to be able to get away with is...
something that *won't* work - but that looks like it might:

```
=<  $.deq(+< a)             ::  create, replace and invoke
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```

We try it and - doh!

    : /~zod/try/31/bin/hec/hoon
    ~zod/try=> :hec 42
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[2 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[3 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[4 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[4 8].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[5 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[6 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[8 1].[23 3]>
    ! /~zod/hec/bin/~2014.1.13..22.55.29..8975/try:<[8 5].[8 16]>
    ! type-fail
    ! exit

(This means we had a type failure between columns 5 and 16 of line
8, by the way.  More on troubleshooting errors later.)

Because when we wrote `deq(+< a)`, we didn't mean: compute `deq`,
then modify the `+<` of its result to be `a`.  We meant: compute
`deq`, with the `+<` of the core that contains `deq` - ie, with
the `+<` of the *library* - changed to `a`.  Needless to say,
that's not what we want at all.

The change in `%=` - remember that `deq(+< a)` is a short form
for `%=(deq +< a)` - is always *before* the invocation.
So we can't do this as easily as it looks like we should be able
to do it - because we can't edit the gate until we build it.

Fortunately, we have a synthetic hoon to do this.  What hoon is
(deq a), anyway?  It's `%-`, `cenhep`, `%cnhp`:

```
=<  %-  deq
    a
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```

Let's look at the definition of `%cnhp`, our function caller.
Here are the relevant clips from `hoon.hoon`:
 
    ++  twig
      |%  [%cnhp p=twig q=tusk]
      ==

    ++  tusk  (list twig)

    ++  open
      ^-  twig
      ?-    gen
          [%cnhp *]
        ?~(q.gen [%tsgr p.gen [%cnzy %$]] [%cncl p.gen [%cltr q.gen]])
      ::
          [%cncl *]  
        [%cnsg [%$ ~] p.gen q.gen]
      ::
          [%cnsg *]  
        [%cntr p.gen q.gen [[[[%& 6] ~] r.gen] ~]]
      ::
          [%cntr *]
        :+  %tsls
          q.gen
        :+  %cnts
          (weld p.gen `wing`[[~ 2] ~])
        (turn r.gen |=([p=wing q=twig] [p [%tsgr [~ 3] q]]))
      ==

You're not expected to understand this.  At least, not yet.  But
we see that `%-`, so far from being a primitive, actually is a
special case of `%:`, which is a special case of `%~`, which is a
special case of `%*`, which seems to do... something.

Which probably makes you think `%cnhp` is insanely complicated.
Actually it just shows what an interesting family of things
which are like function calls, but which are not function calls,
we have here in Hoon.  Defining `%cnhp` this way saves code, but
it doesn't help us understand the humble function call.

If we just wanted %cnhp to be simple, we'd define it this way:

    ++  twig
      |%  [%cnhp p=twig q=twig]
      ==

    ++  open
      ^-  twig
      ?-    gen
          [%cnhp *]
        :+  %tsls  q.gen
        :+  %tsgl  [%cnzy %$]
        [%cnhp [[%& 2] ~] [[[%& 6] ~] [%tsgl [~ 3] p.gen]] ~]
      ==

This also is not terribly readable.  Let's translate it into the
code that we'd write if we were writing this macro out by hand:

```
=<  =+  deq               ::  create the gate
    $.-(+< =>(+ a))       ::  replace the sample and invoke
|%
++  deq
  =+  x=0
  |.  =+  y=0
      |-  ?:  =(x +(y))
            y
          $(y +(y))
--
```

Compare this to the working example above.  The main difference
is that this is what a Lisp fan would call a "hygienic macro."
It does not create private symbols of its own, or if it does they
are not visible to the programmer.  For instance, when we write

    =+  deq

instead of

    =+  foo=deq

we see how wrong it is to think of the latter as "declaring a
variable `foo` and assigning it to `deq`."  Actually, we have no
need within a synthetic hoon to bind a name.  After the `=+`, we
can reach the gate with just `-`. 

But we cannot simply replace the sample with `a` - because the
programmer who wrote `a` meant to write a twig against the
original subject, not against the cell of gate and subject.

Fortunately, `=>(+ a)` gets us our original subject back - and
`%-` works exactly as if it was a natural hoon.  That's hygiene.

###Sequel hook###

So is this the right way to write decrement (assuming we didn't
already have decrement)?  No - there is actually a hoon designed
specifically for building gates.  

The right code (which we saw earlier) is:

```
=<  (deq a)
|%
++  deq
  |=  x=@
  =|  y=@
  |-  ?:  =(x +(y))
        y
      $(y +(y))
--
```

But this requires us to understand `x=@` - which *isn't even a
twig*.  Rather, it's something else, called a `tile`.  We'll make
friends with the tiles in the next chapter.

For those with classic FP experience, it's very tempting to read
`|=` (`bartis`, `%brts`) as "lambda."  I hope it's clear by now
that the difference between Hoon and classic FP languages is like
the difference between a bat and a bird.  Both have wings and use
them to fly, but below that level everything is different.
