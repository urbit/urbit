::
::::  /hoon/core/react-snip/mar
  ::
/?  314
/=  react-elems
  /;  sa  /~ 
  :~  %a  %abbr  %address  %area  %article  %aside  %audio  %b  %base
    %bdi  %bdo  %big  %blockquote  %body  %br  %button  %canvas  %caption
    %cite  %code  %col  %colgroup  %data  %datalist  %dd  %del  %details
    %dfn  %dialog  %div  %dl  %dt  %em  %embed  %fieldset  %figcaption
    %figure  %footer  %form  %h1  %h2  %h3  %h4  %h5  %h6  %head  %header
    %hr  %html  %i  %iframe  %img  %input  %ins  %kbd  %keygen  %label
    %legend  %li  %link  %main  %map  %mark  %menu  %menuitem  %meta
    %meter  %nav  %noscript  %object  %ol  %optgroup  %option  %output  %p
    %param  %picture  %pre  %progress  %q  %rp  %rt  %ruby  %s  %samp
    %script  %section  %select  %small  %source  %span  %strong  %style
    %sub  %summary  %sup  %table  %tbody  %td  %textarea  %tfoot  %th
    %thead  %time  %title  %tr  %track  %u  %ul  %var  %video  %wbr
    %circle  %defs  %ellipse  %g  %line  %linear-gradient  %mask  %path
    %pattern  %polygon  %polyline  %radial-gradient  %rect  %stop  %svg
    %text  %tspan
  ==
!:
::::
  ::
|_  own=manx
::
++  vale  ~(has in react-elems)
++  grow                                                ::  convert to
  |%
  ++  tape
    ;:  weld
      "React.createElement("
      =*  tan  n.g.own
      ?^  tan  !!                                       ::  namespaces unsupported
      ?:  (vale tan)
        "'{(trip tan)}'"
      (trip tan)
      ::
      ", "
      =-  (pojo (jobe (turn a.g.own -)))
      |=  [a=mane b=^tape]
      =.  a  ?+(a a %class 'className')
      ?^(a !! [a (jape b)])
      ", "
      ::
      =-  ~(ram re %rose [", " "[" "]"] (turn c.own -))
      |=  a=manx 
      ?:  ?=(_:/(**) a)
        leaf/(pojo (jape v.i.a.g.a))
      leaf/tape(own a)
      ::
      ")"
    ==
  ++  react-snip-js  (crip tape)
::   ++  js  react-snip-js                                 ::  convert to %js
  ++  mime  [/text/javascript (taco react-snip-js)]                ::  convert to %mime
  --
++  grab  |%                                            ::  convert from
          ++  noun  manx                                ::  clam from %noun
          ++  snip  |=  a=manx  a
--        --
