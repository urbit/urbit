::
::::  /hook/door/sole-effect/mar
  ::
/?    314
/-    *sole
!:
::::
  ::
|%
++  mar-sole-change                       ::  XX  dependency
  |_  cha=sole-change
  ++  grow
    |%  ++  json
      ^-  ^json
      =+  cha
      =<  (jobe ted/(. ted) ler/a/~[(jone own.ler) (jone his.ler)] ~)
      |=  ted=sole-edit
      ?-  -.ted
        %nop  [%s 'nop']
        %mor  [%a (turn p.ted ..$)]
        %del  (joba %del (jone p.ted))
        %set  (joba %set (jape (tufa p.ted)))
        %ins  (joba %ins (jobe at/(jone p.ted) new/s/(tuft q.ted) ~))
      ==
    --  
  --
++  wush
  |=  [wid=@u tan=tang]
  ^-  tape
  =+  rolt=|=(a=wall `tape`?~(a ~ :(weld i.a "\0a" $(a t.a))))
  (rolt (turn tan |=(a=tank (rolt (wash 0^wid a)))))
::
--
!:
|_  sef=sole-effect
::
++  grab                                                ::  convert from
  |%
  ++  noun  sole-effect                                 ::  clam from %noun
  --
++  grow
  |%
  ++  json
    ^-  ^json
    ?+  -.sef  ~|(unsupported-effect/-.sef !!)
      %mor  [%a (turn p.sef |=(a=sole-effect json(sef a)))]
      %err  (joba %hop (jone p.sef))
      %txt  (joba %txt (jape p.sef))
      %tan  (joba %tan (jape (wush 160 p.sef)))
      %det  json:~(grow mar-sole-change +.sef)
      %pro  (jobe vis/b/vis.sef tag/s/tag.sef cad/(jape cad.sef) ~)
      ?(%bel %clr %nex)  (joba %act %s -.sef)
    ==
  --
--
