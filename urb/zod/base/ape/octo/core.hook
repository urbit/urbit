::                                                      ::  ::
::::  /hook/core/acto/ape                               ::  ::  dependencies
  ::                                                    ::  ::
/?  310                                                 ::  arvo version
/-  *sole, *octo                                        ::  structures
/+  sole, octo                                          ::  libraries
::                                                      ::  ::
::::                                                    ::  ::  server
  !:                                                    ::  ::
=>  |%                                                  ::  arvo structures
    ++  axle  ,[eye=face rem=(unit ship) gam=game]      ::  agent state
    ++  card  $%  [%diff lime]                          ::  update
                  [%quit ~]                             ::  cancel
                  [%peer wire dock path]                ::  subscribe
                  [%poke wire dock pear]                ::  send move
                  [%pull wire dock ~]                   ::  unsubscribe
              ==                                        ::
    ++  face  (pair (list ,@c) (map bone sole-share))   ::  interface
    ++  lime  $%  [%sole-effect sole-effect]            ::  :sole update
                  [%octo-game game]                    ::  :octo update
              ==                                        ::
    ++  move  (pair bone card)                          ::  cause and action
    ++  pear  ,[%octo-move point]                       ::  outgoing move
    --                                                  ::
=>  |%                                                  ::  historical state
    ++  axon     $%([%1 axle] [%0 axle-0])              ::
    ++  axle-0  ,[eye=face gam=game-0]                  ::
    ++  game-0  ,[who=? box=board boo=board]            ::
    --                                                  ::
=>  |%                                                  ::  parsers
    ++  colm  (cook |=(a=@ (sub a '1')) (shim '1' '3')) ::  row or column
    ++  come  ;~(plug colm ;~(pfix fas colm))           ::  coordinate
    ++  comb  (pick come ;~(pfix sig (punt fed:ag)))    ::  all command input
    ++  cope  |=(? ?:(+< (stag %| (cold ~ sig)) comb))  ::  with wait mode
    --                                                  ::
|_  [hid=hide moz=(list move) %1 axle]                  ::  per agent
++  et                                                  ::
  |_  [from say=sole-share]                             ::  per console client
  ++  abet  +>(q.eye (~(put by q.eye) ost say))         ::  continue
  ++  amok  +>(q.eye (~(del by q.eye) ost))             ::  discontinue
  ++  beep  (emit %bel ~)                               ::  bad user
  ++  cusp  (cope !ept:here)                            ::  parsing rule
  ++  delt  |=  cal=sole-change                         ::  input line change
            =^  cul  say  (remit:sole cal good)         ::
            ?~  cul  (park:abet(p.eye buf.say) | ~)     ::
            abet:beep:(emit det/u.cul)                  ::
  ++  emit  |=  fec=sole-effect  ^+  +>                 ::  send effect
            +>(moz [[ost %diff %sole-effect fec] moz])  ::  
  ++  good  |=((list ,@c) -:(rose (tufa +<) cusp))      ::  valid input
  ++  here  ~(. go src gam)                             ::  game core
  ++  kick  |=  a=point  =^  dud  gam  ~(m at:here a)   ::  apply move
            ?.(dud abet:beep (kind res:here a))         ::
  ++  kind  |=  [(unit tape) point]                     ::  successful move
            =.  +>  ?~(rem +> (send +<+))               ::
            (park:abet(gam ?^(+<- *game gam)) %2 +<-)   ::
  ++  line  =^  cal  say  (transmit:sole set/p.eye)     ::  update command
            (emit %det cal)                             ::
  ++  make  =+  dur=(rust (tufa p.eye) (punt comb))     ::  apply command
            ?~  dur  abet:beep                          ::
            ?~  u.dur  abet:(play %2)                   ::
            =.  +  line(p.eye ~)  ?-  +>-.dur           ::
            &  (kick +>+.dur)  |  (plan +>+.dur)  ==    ::
  ++  mean  |=((unit tape) ?~(+< +> (emit txt/+<+)))    ::  optional message
  ++  play  |=  lev=?(%0 %1 %2)                         ::  update by level
            ?-(lev %0 +>, %1 line, %2 line:show:prom)   ::
  ++  plow  |=  [lev=?(%0 %1 %2) mus=(unit tape)]       ::  complete print
            abet:(mean:(play lev) mus)                  ::
  ++  prom  (emit %pro %& %octo stat)                   ::  update prompt
  ++  plan  |=  wid=(unit ship)                         ::  subscribe to peer
            ?~  wid  (park:stop:abet(gam *game) %2 ~)   ::
            ?^(rem abet:beep (link:abet u.wid))         ::
  ++  rend  (turn `wall`tab:here |=(tape txt/+<))       ::  table print
  ++  send  |=  point  =.  +>+>  %-  dish               ::  upload move
            poke/((like +.rem) %octo-move +<)  +>       ::
  ++  show  (emit %mor rend)                            ::  update board
  ++  sole  ~(. cs say)                                 ::  console core
  ++  stat  ^-  tape  =+  ike=~[(icon who.gam)]         ::  status line
            %-  zing  :~                                ::
              ?~(rem "" "@{(scow %p u.rem)}")           ::
              ?~(aud.gam "" vew:here)                   ::
              ?:  !ept:here  " ({ike}'s turn) "         ::
              ": {ike} (row/col): "                     ::  
            ==                                          ::
  ++  work  |=  act=sole-action                         ::  console input
            ?:(?=(%det -.act) (delt +.act) make)        ::
  --                                                    ::
++  abet  [(flop moz) .(moz ~)]                         ::  resolve core
++  dump  |=(mov=move %_(+> moz [mov moz]))             ::  send move
++  dish  |=(cad=card (dump 0 cad))                     ::  forward move
++  flet  |=(from ~(. et +< (~(got by q.eye) ost)))     ::  in old client
++  fret  |=(from ~(. et +< *sole-share))               ::  in new client
++  like  |=(a=ship |*(* [/octo [a %octo] +<]))         ::  message to friend
++  link  |=  a=ship                                    ::  subscribe to friend
          (dish(rem `a) %peer ((like a) /octo/net))     ::  
++  lose  |=(from ?^(rem +> +>(gam ~(out go src gam)))) ::
++  meet  |=(from ?^(rem +> +>(gam ~(inn go src gam)))) ::
++  pals  %+  turn  (pale hid (prix /sole))  |=  sink   ::  per console 
          [[p=p.+< q=q.+<] r=(~(got by q.eye) p.+<)]    ::
++  park  |=  [lev=?(%0 %1 %2) mus=(unit tape)]         ::  update all
          =.  +>  ?:(=(%2 lev) push +>)                 ::  
          =+  pals                                      ::
          |-  ^+  +>.^$  ?~  +<  +>.^$                  ::
          $(+< t.+<, +>.^$ (~(plow et i.+<) lev mus))   ::
++  push  =+  pey=(pale hid (prix /octo))  |-  ^+  +>   ::  update friends
          ?~(pey +> $(pey t.pey, +> (sell p.i.pey)))    ::
++  sell  |=(bone (dump +< %diff %octo-game gam))       ::  update friend
++  stop  ?~(rem . (dish pull/((like +.rem) ~)))        :: 
::                                                      ::  ::
::::                                                    ::  ::  handlers
  ::                                                    ::  ::
++  coup-octo                                           ::  move acknowledge
          |=  [then saw=(unit tang)]  =<  abet          ::
          ?~(saw +> (park %0 `"move failed"))           ::
++  diff-octo-game                                      ::  friend update
          |=  [then gam=game]  =<  abet                 ::
          ?:  =(^gam gam)  +>                           ::
          (park(gam gam) %2 ~)                          ::
++  peer-octo-net                                       ::  urbit peer
          |=  [from path]  =<  abet                     ::
          (park:(meet +<-) %2 `"net from {<src>}")      ::
++  peer-sole                                           ::  console subscribe
          |=  [from pax=path]  =<  abet                 ::
          (plow:(fret:(meet +<-) +<-) %2 ~)             ::
++  poke-sole-action                                    ::  console input
          |=  [from act=sole-action]  =<  abet          ::
          (work:(flet +<-) act)                         ::
++  poke-octo-move                                      ::
          |=  [from wha=point]  =<  abet                ::
          =^  dud  gam  ~(m ~(at go src gam) wha)       ::
          ?>  dud  =+  mus=~(res go src gam)            ::
          (park(gam ?^(mus *game gam)) %2 mus)          ::
++  prep  |=  [from old=(unit ,[(list move) axon])]     ::  initialize
          =<  abet  ?~  old  +>                         ::
          =<  (park %2 ~)                               ::
          ?-  -.+>.old                                  ::
            %1  +>(+<+ u.old)                           ::
            %0  !!                                      ::
          ==                                            ::
++  pull-octo                                           ::  unsubscribe
          |=  [from *]  =<  abet                        ::
          (lose +<-)                                    ::
++  pull-sole                                           ::  disconnect console
          |=  [from *]  =<  abet                        ::
          amok:(flet:(lose +<-) +<-)                    ::
++  quit-octo                                           ::  unlinked by friend
          |=(then abet:(park(rem ~) %0 `"removed"))     ::
++  reap-octo                                           ::  linked to friend
          |=  [then saw=(unit tang)]  =<  abet          ::
          ?~  saw  (park %0 `"linked to {<src>}")       ::
          (park(rem ~) %0 `"blocked by {<src>}")        ::
--
