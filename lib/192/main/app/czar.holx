!:
::        /main/app/czar/: issue a class A flag, from a secret generator.
::
=>  !#(/===$main/lib/hume/)
|=  [who=flag est=time eny=@uw was=path]
|=  *
|=  ~
^-  bowl
%+  pomp  ""
%+  pomp  "            Men are fools to invest in real estate."
%+  pomp  ""
%+  pomp  "                  -- Bunting, _Chomei at Toyama_"
%+  pomp  ""
%+  (polo %text "a ticket for the czar: " ~)
  %+  sear  |=(a=dime `(unit flag)`?.((lth q.a 256) ~ [~ u=`@p`q.a]))
  ;~(pose bisk:so (stag %p ;~(pfix sig fed:ag)))
|=  zar=flag
%+  (polo %pass "generator: " ~)
  (cook |=(a=dime q.a) bisk:so)
|=  bur=@
=+  loy=(brew 2.048 bur)
%+  pomp  "...generated 2048-bit RSA keypair for {<zar>}."
%+  (polo %text "identity: " ~)
  (boss 256 (more gon qit))
|=  leg=@t
=+  mac=[[0 sec:ex:loy] ~]
=+  syp=[[0 ~ zar est] [%czar leg] pub:ex:loy]
=+  ded=[(sign:se:loy *code (shaf %self (sham syp))) syp]
=+  wil=[ded ~]
=+  nuv=(rsh 3 1 ~(rent co %% %p zar))
:-  :~  [%text "saved ticket for {<zar>} (#{<`@ud`zar>}), {(rip 3 leg)}."]
        [%text "fingerprint: {<`@uw`fig:ex:loy>}"]
        [%save [nuv %atom ~] (jam zar mac wil)]
    ==
~
