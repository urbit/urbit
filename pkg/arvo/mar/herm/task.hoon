::  task: herm task for passthrough to dill
::
/-  herm
/+  dill
::
|_  =task:herm
++  grad  %noun
::  +grab: convert from
::
++  grab
  |%
  ++  noun  task:herm
  ::
  ++  json
    |=  jon=^json
    ^+  task
    ~|  jon
    ?>  ?=([%o *] jon)
    =+  ses=(~(got by p.jon) 'session')
    ?>  ?=([%s *] ses)
    :-  ?:  =('' p.ses)  %$
        (slav %tas p.ses)
    =.  p.jon  (~(del by p.jon) 'session')
    %.  jon
    =,  dejs:format
    |^  task
    ++  task
      %-  of
      :~  belt+belt:dejs:^dill
          blew+(ot 'w'^ni 'h'^ni ~)
          hail+ul
          open+(ot 'term'^(se %tas) 'apps'^(ar gill) ~)
          shut+ul
      ==
    ::
    ++  gill
      (ot 'who'^(se %p) 'app'^(se %tas) ~)
    --
  --
::  +grow: convert to
::
++  grow
  |%
  ++  noun  task
  --
--
