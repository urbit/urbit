::  Perform a remote scry
::
/-  spider
/+  strandio
=,  strand=strand:spider
^-  thread:spider
::
|=  arg=vase
=/  m  (strand ,vase)
^-  form:m
=+  !<([~ =spar:ames sec=$@(~ ?([%chum ~] [%shut idx=@ key=@]))] arg)
;<    ~   bind:m
  ?@  sec  (keen:strandio /peek spar ~)
  ?-  -.sec
    %chum  (chum:strandio /peek spar)
    %shut  (keen:strandio /peek spar `+.sec)
  ==
;<  [=spar:ames roar=(unit roar:ames)]  bind:m  (take-tune:strandio /peek)
:: ;<  =sage:mess:ames  bind:m  (take-sage:strandio /peek)  :: restore in 409k

=/  =sage:mess:ames
  :-  spar
  ?~  roar  ~
  ?~  q.dat.u.roar  ~
  u.q.dat.u.roar
?~  q.sage
  (pure:m !>([leaf+"... empty response ..." ~]))
::
;<  =bowl:spider  bind:m  get-bowl:strandio
=+  .^  =dais:clay  %cb
        /(scot %p our.bowl)/[q.byk.bowl]/(scot %da now.bowl)/[p.q.sage]
    ==
=/  res  (mule |.((vale.dais q.q.sage)))
?.  ?=(%| -.res)
  (pure:m p.res)
~|(%keen-mark-fail (mean leaf+"-peek: ames vale fail {<mark>}" p.res))
