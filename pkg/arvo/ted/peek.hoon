/-  spider
/+  strandio
=,  strand=strand:spider
^-  thread:spider
=>  |%  +$  out  $%  [%sage =sage:mess:ames]
                     [%tune (pair spar:ames (unit roar:ames))]
                 ==
    --
::
|=  arg=vase
=/  m  (strand ,vase)
^-  form:m
=+  !<([~ =spar:ames sec=$@(%chum (unit [idx=@ key=@]))] arg)
;<    ~   bind:m
  ?@  sec  (chum:strandio /peek spar)
  (meen:strandio /peek spar sec)
;<  =out  bind:m  (take-message:strandio /peek)
=/  =sage:mess:ames
  ?-  -.out
    %sage  sage.out
    %tune  :-  p.+.out
           ?~  q.+.out  ~
           ?~  q.dat.u.q.+.out  ~
           u.q.dat.u.q.+.out
  ==
?~  q.sage
  (pure:m !>(~))
::
;<  =bowl:spider  bind:m  get-bowl:strandio
=+  .^  =dais:clay  %cb
        /(scot %p our.bowl)/[q.byk.bowl]/(scot %da now.bowl)/[p.q.sage]
    ==
=/  res  (mule |.((vale.dais q.q.sage)))
?.  ?=(%| -.res)
  (pure:m p.res)
~|(%keen-mark-fail (mean leaf+"-keen: ames vale fail {<mark>}" p.res))
