::  Get metadata about an Ames connection
::
/-  spider
/+  strandio
=,  strand=strand:spider
^-  thread:spider
^-  thread:spider
::
|=  arg=vase
=/  m  (strand ,vase)
^-  form:m
=+  !<([~ =spar:ames] arg)
;<  ~                                   bind:m  (meta:strandio /meta spar)
:: ;<  =sage:mess:ames  bind:m  (take-sage:strandio /meta)  :: restore in 409k
;<  [=spar:ames roar=(unit roar:ames)]  bind:m  (take-tune:strandio /puek)
;<  ~                                   bind:m  (yawn:strandio /meta spar)
=/  =sage:mess:ames
  :-  spar
  ?~  roar  ~
  ?~  q.dat.u.roar  ~
  u.q.dat.u.roar
?~  q.sage
  (pure:m !>([leaf+"... empty response ..." ~]))
?>  ?=(%message p.q.sage)
%-  pure:m  !>
?+  -.q.q.sage  ~|(q.q.sage !!)
  %clos  ["flow in closing? " "{<;;(? +.q.q.sage)>}"]
  %gone  ["flow is corked?" "%.y"]
  %line  ["migration line: " "{<;;(@ud +.q.q.sage)>}"]
  %lods  ["unacked payloads: " "{<;;(@ud +.q.q.sage)>}"]
  %next  ["next payload: " "{<;;(@ud +.q.q.sage)>}"]
  %last  ["last acked: " "{<;;(@ud +.q.q.sage)>}"]
  %naxp  ["message nacked? " "{<;;(? +.q.q.sage)>}"]
  %curr  ["current message: " "{<;;(@ud +.q.q.sage)>}"]
  %whey  ["flow size: " "{<;;([boq=@ud siz=@ud] +.q.q.sage)>}"]
==
